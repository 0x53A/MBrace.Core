namespace MBrace.Runtime

open System
open System.Runtime.Serialization

open MBrace.Core
open MBrace.Core.Internals

/// Represents a log entry that has been generated by an MBrace process
[<Struct; DataContract>]
type CloudLogEntry =

    [<DataMember(Name = "DateTime", Order = 0)>]
    val private dateTime : DateTime
    [<DataMember(Name = "Message", Order = 1)>]
    val private message : string
    [<DataMember(Name = "TaskId", Order = 2)>]
    val private taskId : string
    [<DataMember(Name = "WorkerId", Order = 3)>]
    val private workerId : string
    [<DataMember(Name = "JobId", Order = 4)>]
    val private jobId : CloudJobId

    new (taskId : string, workerId : string, jobId : CloudJobId, dateTime : DateTime, message : string) =
        { taskId = taskId ; workerId = workerId ; jobId = jobId ; dateTime = dateTime ; message = message }

    /// Task identifier for log entry
    member e.TaskId = e.taskId
    /// Worker identifier for log entry
    member e.WorkerId = e.workerId
    /// Job identifier for log entry
    member e.JobId = e.jobId
    /// Date of log entry creation
    member e.DateTime = e.dateTime
    /// Message of log entry
    member e.Message = e.message

    /// <summary>
    ///     Prints log entry as a single line string.
    /// </summary>
    /// <param name="cle">Cloud log entry to be printed.</param>
    /// <param name="showDate">Show date at the beginning. Defaults to false.</param>
    static member Format(cle : CloudLogEntry, ?showDate : bool) =
        if defaultArg showDate false then
            let date = cle.dateTime.ToString("yyyy-MM-dd H:mm:ss")
            sprintf "[%s][Worker:%s][Job:%O] %s" date cle.workerId cle.jobId cle.message
        else 
            sprintf "[Worker:%s][Job:%O] %s" cle.workerId cle.jobId cle.message

/// CloudLogger instance used for logging a specific job
type ICloudJobLogger =
    inherit ICloudLogger
    inherit IDisposable

/// Abstraction used for managing cloud log entries
type ICloudLogManager =
    /// <summary>
    ///     Gets a CloudLogger instance specific to supplied CloudJob.
    /// </summary>
    /// <param name="worker">Current worker identifier.</param>
    /// <param name="job">Current cloud job.</param>
    abstract CreateJobLogger : worker:IWorkerId * job:CloudJob -> Async<ICloudJobLogger>

    /// <summary>
    ///     Asynchronoulsy fetches all log entries related to task of provided id.
    /// </summary>
    /// <param name="taskId">Task identifier to be queried.</param>
    abstract GetAllCloudLogsByTask : taskId:string -> Async<seq<CloudLogEntry>>

    /// <summary>
    ///     Asynchronously returns an observable for cloud logs generated by task id.
    /// </summary>
    /// <param name="taskId">Task identifier to be queried.</param>
    abstract GetCloudLogObservableByTask : taskId:string -> Async<IObservable<CloudLogEntry>>